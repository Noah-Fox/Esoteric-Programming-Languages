(AG>>>>>>)

>>>-[[>>>+<<<-]+>>>[>>>+<<<-]+>>>-]<<<[<<<]     //create array (end on G)
>>>[<<+++++++++>++++>>>>]<<<[<<<]               //set all X_cells to 9 and Y_cells to 4 (end on G)

>>>-
A  >--->+>
   >------>-->
P  >->---->
a  >>+++>
r  >++>>
t  >++>++>
r  >++>>
i  >+>+>
d  >+>---->
g  >+>->
e  >+>--->
   >------>-->
i  >+>+>
n  >++>---->
   >------>-->
a  >>+++>
   >------>-->
P  >->---->
e  >+>--->
a  >>+++>
r  >++>>
   >------>-->
T  >->>
r  >++>>
e  >+>--->
e  >+>--->
:  >----->++>
\n >-------->---->
>>>-
T  >->>
w  >++>+++++>
o  >++>--->
   >------>-->
T  >->>
u  >++>+++>
r  >++>>
t  >++>++>
l  >+>++++>
e  >+>--->
   >------>-->
D  >--->++++>
o  >++>--->
v  >++>++++>
e  >+>--->
s  >++>+>
;  >----->>
   >------>-->
a  >>+++>
n  >++>---->
d  >+>---->
\n >-------->---->
>>>-
T  >->>
h  >+>>
r  >++>>
e  >+>--->
e  >+>--->
   >------>-->
F  >-->---->
r  >++>>
e  >+>--->
n  >++>---->
c  >>+++++>
h  >+>>
   >------>-->
H  >-->-->
e  >+>--->
n  >++>---->
s  >++>+>
;  >----->>
\n >-------->---->
>>>-
F  >-->---->
o  >++>--->
u  >++>+++>
r  >++>>
   >------>-->
C  >--->+++>
a  >>+++>
l  >+>++++>
l  >+>++++>
i  >+>+>
n  >++>---->
g  >+>->
   >------>-->
B  >--->++>
i  >+>+>
r  >++>>
d  >+>---->
s  >++>+>
;  >----->>
\n >-------->---->
>>>-
F  >-->---->
i  >+>+>
v  >++>++++>
e  >+>--->
   >------>-->
G  >-->--->
o  >++>--->
l  >+>++++>
d  >+>---->
   >------>-->
R  >->-->
i  >+>+>
n  >++>---->
g  >+>->
s  >++>+>
;  >----->>
\n >-------->---->
>>>-
S  >->->
i  >+>+>
x  >+++>---->
   >------>-->
G  >-->--->
e  >+>--->
e  >+>--->
s  >++>+>
e  >+>--->
~  >----->+>
a  >>+++>
~  >----->+>
L  >-->++>
a  >>+++>
y  >+++>--->
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
S  >->->
e  >+>--->
v  >++>++++>
e  >+>--->
n  >++>---->
   >------>-->
S  >->->
w  >++>+++++>
a  >>+++>
n  >++>---->
s  >++>+>
~  >----->+>
a  >>+++>
~  >----->+>
S  >->->
w  >++>+++++>
i  >+>+>
m  >+>+++++>
m  >+>+++++>
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
E  >--->+++++>
i  >+>+>
g  >+>->
h  >+>>
t  >++>++>
   >------>-->
M  >-->+++>
a  >>+++>
i  >+>+>
d  >+>---->
s  >++>+>
~  >----->+>
a  >>+++>
~  >----->+>
M  >-->+++>
i  >+>+>
l  >+>++++>
k  >+>+++>
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
N  >-->++++>
i  >+>+>
n  >++>---->
e  >+>--->
   >------>-->
L  >-->++>
a  >>+++>
d  >+>---->
i  >+>+>
e  >+>--->
s  >++>+>
   >------>-->
D  >--->++++>
a  >>+++>
n  >++>---->
c  >>+++++>
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
T  >->>
e  >+>--->
n  >++>---->
   >------>-->
L  >-->++>
o  >++>--->
r  >++>>
d  >+>---->
s  >++>+>
~  >----->+>
a  >>+++>
~  >----->+>
L  >-->++>
e  >+>--->
a  >>+++>
p  >++>-->
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
E  >--->+++++>
l  >+>++++>
e  >+>--->
v  >++>++++>
e  >+>--->
n  >++>---->
   >------>-->
P  >->---->
i  >+>+>
p  >++>-->
e  >+>--->
r  >++>>
s  >++>+>
   >------>-->
P  >->---->
i  >+>+>
p  >++>-->
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
T  >->>
w  >++>+++++>
e  >+>--->
l  >+>++++>
v  >++>++++>
e  >+>--->
   >------>-->
D  >--->++++>
r  >++>>
u  >++>+++>
m  >+>+++++>
m  >+>+++++>
e  >+>--->
r  >++>>
s  >++>+>
   >------>-->
D  >--->++++>
r  >++>>
u  >++>+++>
m  >+>+++++>
m  >+>+++++>
i  >+>+>
n  >++>---->
g  >+>->
;  >----->>
\n >-------->---->
>>>-
O  >-->+++++>
n  >++>---->
   >------>-->
t  >++>++>
h  >+>>
e  >+>--->
   >------>-->
>>>-
   >------>-->
d  >+>---->
a  >>+++>
y  >+++>--->
   >------>-->
o  >++>--->
f  >+>-->
   >------>-->
C  >--->+++>
h  >+>>
r  >++>>
i  >+>+>
s  >++>+>
t  >++>++>
m  >+>+++++>
a  >>+++>
s  >++>+>
\n >-------->---->
M  >-->+++>
y  >+++>--->
   >------>-->
t  >++>++>
r  >++>>
u  >++>+++>
e  >+>--->
   >------>-->
l  >+>++++>
o  >++>--->
v  >++>++++>
e  >+>--->
   >------>-->
s  >++>+>
e  >+>--->
n  >++>---->
t  >++>++>
   >------>-->
t  >++>++>
o  >++>--->
   >------>-->
m  >+>+++++>
e  >+>--->
\n >-------->---->
>>>-
F  >-->---->
i  >+>+>
r  >++>>
s  >++>+>
t  >++>++>
>>>-
S  >->->
e  >+>--->
c  >>+++++>
o  >++>--->
n  >++>---->
d  >+>---->
>>>-
T  >->>
h  >+>>
i  >+>+>
r  >++>>
d  >+>---->
>>>-
F  >-->---->
o  >++>--->
u  >++>+++>
r  >++>>
t  >++>++>
h  >+>>
>>>-
F  >-->---->
i  >+>+>
f  >+>-->
t  >++>++>
h  >+>>
>>>-
S  >->->
i  >+>+>
x  >+++>---->
t  >++>++>
h  >+>>
>>>-
S  >->->
e  >+>--->
v  >++>++++>
e  >+>--->
n  >++>---->
t  >++>++>
h  >+>>
>>>-
E  >--->+++++>
i  >+>+>
g  >+>->
h  >+>>
t  >++>++>
h  >+>>
>>>-
N  >-->++++>
i  >+>+>
n  >++>---->
t  >++>++>
h  >+>>
>>>-
T  >->>
e  >+>--->
n  >++>---->
t  >++>++>
h  >+>>
>>>-
E  >--->+++++>
l  >+>++++>
e  >+>--->
v  >++>++++>
e  >+>--->
n  >++>---->
t  >++>++>
h  >+>>
>>>-
T  >->>
w  >++>+++++>
e  >+>--->
l  >+>++++>
f  >+>-->
t  >++>++>
h  >+>>

>--------->---->

<<<[[<<<]<[<[-]>[-]>+<]>]       //set all empty elements to 0;0;1; go to G 


>>>[<<[>++++++++++<-]>>>>>]<<<[<<<]     //combine X_Y pairs into char vals (end on G)

(GD<<<)------------     //set D to 4 
[               //while D 

//print "on the"
(DX>>>>)++++++++++++    //set X to 12 
//print single 
    [[(XY>)>>>[>>>]<+<<[<<<]<-](XY>)>>>[>>>]<-]  //go to X_cell of empty element before string of index *X
    >>>>[.>>>]<<[<<<]   //output selected string (end on G)

//print number of day 
(GD<<<)[(DE>)+(EX>>>)+(XD<<<<)-]    //move D to E and X (end on D)
(DE>)[(ED<)+(DE>)-]                 //move E to D (end on E)
(EX>>>)++++++++++++++++++++++++++   //add 14 to X 
//print single 
    [[(XY>)>>>[>>>]<+<<[<<<]<-](XY>)>>>[>>>]<-]  //go to X_cell of empty element before string of index *X
    >>>>[.>>>]<<[<<<]   //output selected string (end on G)


//print "day of :::gave to me"
(GX>)+++++++++++++      //set X to 13 
//print single 
    [[(XY>)>>>[>>>]<+<<[<<<]<-](XY>)>>>[>>>]<-]  //go to X_cell of empty element before string of index *X
    >>>>[.>>>]<<[<<<]   //output selected string (end on G)

(GD<<<)

[(DE>)+(EX>>>)+(XD<<<<)-]       //move D to E and X (end on D)
(DE>)[(ED<)+(DE>)-]             //move E to D (end on E)
(EX>>>)
++++++++++++                    //add 12 to X 
//print_receding
    [[(XY>)>>>[>>>]<+<<[<<<]<-](XY>)>>>[>>>]<-]  //go to X_cell of empty element before string of index *X
    >>>>[>>>]<<[<[<<<]>>>[.>>>]<<<[<<<]<<]  //output selected string and all previous strings (end on G)
(GF<)++++++++++.[-](FD<<)+      //print newline; increment D 
]

